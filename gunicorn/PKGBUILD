# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: Sebastian Lau <lauseb644 at gmail dot com>
# Contributor: pumpkin <pumpkin at mailoo dot org>
# Contributor: Vsevolod Balashov <vsevolod at balashov dot name>

pkgbase=gunicorn
pkgname=('python-gunicorn' 'python2-gunicorn')
pkgver=19.6.0
pkgrel=2
pkgdesc='WSGI HTTP Server for UNIX'
arch=(any)
url='http://gunicorn.org/'
license=(MIT)
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver::https://github.com/benoitc/${pkgbase}/archive/${pkgver}.tar.gz")
sha256sums=('1e0de4957bea60bfcff5215664bdfc3cf02c78e2aae9586766a9b4b437aebbb0')

prepare() {
  cp -a "${srcdir}/${pkgbase}-${pkgver}"{,-python2}
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${pkgbase}-${pkgver}-python2"
  python2 setup.py build
}


package_python-gunicorn() {
  depends=('python')
  provides=('gunicorn' 'python-gunicorn')
  optdepends=(
    'python-eventlet: For asynchronous request handling with eventlet.'
    'python-gevent: For asynchronous request handling with gevent.'
  )

  cd "${srcdir}/${pkgbase}-${pkgver}"

  python setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1

  rm -r "${pkgdir}/usr/bin/gunicorn_django" "${pkgdir}/usr/bin/gunicorn_paster"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-gunicorn() {
  depends=('python2')
  provides=('python2-gunicorn' 'gunicorn-python2')
  optdepends=(
    'python2-eventlet: For asynchronous request handling with eventlet.'
    'python2-gevent: For asynchronous request handling with gevent.'
  )

  cd "${srcdir}/${pkgbase}-${pkgver}-python2"

  python setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1

  rm -r "${pkgdir}/usr/bin/gunicorn_django" "${pkgdir}/usr/bin/gunicorn_paster"
  mv "${pkgdir}/usr/bin/gunicorn" "${pkgdir}/usr/bin/python2-gunicorn"
  ln -s "/usr/bin/python2-gunicorn" "${pkgdir}/usr/bin/gunicorn-python2"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
